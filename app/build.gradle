apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    def CONFIG = { k -> "\"${project.properties.get(k)}\"" }

    defaultConfig {
        applicationId "ee.mtiidla.swimresult"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
            versionNameSuffix "-demo"
        }
        full {
            buildConfigField "String", "API_URL", CONFIG("ee.mtiidla.swimresult.api.url")
            buildConfigField "String", "API_SECRET", CONFIG("ee.mtiidla.swimresult.api.secret")
            dimension "version"
            versionNameSuffix "-full"
        }
    }

    // Add the kapt generated source as sourceSet
    android.applicationVariants.all { variant ->
        variant.addJavaSourceFoldersToModel()
        def kotlinGenerated = file("$buildDir/generated/source/kaptKotlin/${variant.name}")
        variant.addJavaSourceFoldersToModel(kotlinGenerated)
    }
}

androidExtensions {
    // Android Extensions plugin provides Parcelable implementation generator as an experimental feature.
    experimental = true
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.hannesdorfmann:adapterdelegates4:4.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.0.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.squareup.moshi:moshi:1.8.0"
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.google.dagger:dagger:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'

    // test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
